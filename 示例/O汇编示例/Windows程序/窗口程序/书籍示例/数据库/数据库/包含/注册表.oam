//注册表文件

.只读段
{    
    字节 扩展名子键..      = ".wwd"
    字节 扩展名子键属性..  = "wwdfile"
    字节 扩展名关联子键..  = "wwdfile\\shell\\open\\command"
    字节 关联参数..        = " \"%1\""
    字节 关联提示框标题..  = "关联提示"
    字节 成功关联提示..    = "关联wwd文件成功"
    字节 取消关联提示..    = "取消与wwd文件的关联"
}

.代码段
{
    函数 设置关联()
    {
	    双字 h键
	    
	    //添加"扩展名子键"
	    累加32 @= h键 //lea eax,h键
	    注册表创建键(HKEY_CLASSES_ROOT,取地址 扩展名子键,累加32) //RegCreateKey	    
	    如果(累加32 == ERROR_SUCCESS)
         {
             //设置扩展名子键属性
		  高级注册表设置值(h键,0,0, ...
		    REG_SZ,取地址 扩展名子键属性,取大小 扩展名子键属性) //RegSetValueEx

		  注册表关闭键(h键) //RegCloseKey
	    }
         否则
         {
             返回 假
         }         
         
         //添加"扩展名关联子键"  wwdfile\shell\open\command
         累加32 @= h键 //lea eax,h键
	    注册表创建键(HKEY_CLASSES_ROOT,取地址 扩展名关联子键,累加32) //RegCreateKey
	    如果(累加32 == ERROR_SUCCESS)
         {
             字节 本程序文件名..256
             双字 程序文件名长度
             
             //获取本程序的文件名
             累加32 @= 本程序文件名 //lea eax,本程序文件名
             获取模块文件名(0,累加32,256)
             累加32 @= 本程序文件名 //lea eax,本程序文件名
		  字符串联接(累加32,取地址 关联参数) //lstrcat
		  累加32 @= 本程序文件名 //lea eax,本程序文件名
		  字符串长度(累加32) //lstrlen
		  累加32 ++  //inc eax
		  程序文件名长度 = 累加32 //mov 程序文件名长度,eax
		  
		  //设置键属性为本程序文件名并加上"%1"参数
		  累加32 @= 本程序文件名 //lea eax,本程序文件名
		  高级注册表设置值(h键,0,0, ...
		      REG_EXPAND_SZ,累加32,程序文件名长度) //RegSetValueEx
		  注册表关闭键(h键) //RegCloseKey
	    }
         否则
         {
             返回 假
         }
         
         提示框(0,取地址 成功关联提示,取地址 关联提示框标题,信息框_OK) //MessageBox

         返回  真
    }
    
    函数 取消关联()
    {
        //使用shell函数
        壳删除键(HKEY_CLASSES_ROOT,取地址 扩展名子键) //SHDeleteKey
        如果(累加32 != ERROR_SUCCESS)
        {
            返回  假
        }
        壳删除键(HKEY_CLASSES_ROOT,取地址 扩展名子键属性) //SHDeleteKey
        如果(累加32 != ERROR_SUCCESS)
        {
            返回  假
        }
        
        提示框(0,取地址 取消关联提示,取地址 关联提示框标题,信息框_OK) //MessageBox
        
        返回  真
    }
}
