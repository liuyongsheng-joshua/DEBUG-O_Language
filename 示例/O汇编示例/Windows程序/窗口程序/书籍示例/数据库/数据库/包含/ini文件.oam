//ini文件操作,此文件是对选项对话框的操作
//应该将本文件包含在选项对话框代码文件内


.只读段
{
    字节  ini文件名..    = "选项.ini"
    字节  树节名..       = "树节"    
    字节  组合框节名..   = "组合框节"
    字节  列表节名..     = "列表节"
    字节  列表全选标识.. = "全选"
    字节  其它选项名..   = "其它选项"
    字节  编辑框节名..   = "编辑框文本"
    字节  最前标识名..   = "最前"
    字节  开始标识名..   = "开始"    
    字节  格式化缓存..   = "%d"
}

.数据段
{
    字节  ini文件路径名..256
}

.代码段
{
    函数  获得INI文件路径名()
    {
        如果(ini文件路径名:0 == 0)
        {
            获取模块文件名(模块句柄,取地址 ini文件路径名,256) //GetModuleFileName
            
            累加32 = 取地址 ini文件路径名 //mov eax,offset ini文件路径名
            循环([字节|累加32] != 0) //通过循环得到最后一个左斜杠
            {   
                如果([字节|累加32] == '\\') //cmp ptr byte [eax],'\\'   jcc
                {
                    基数32 = 累加32  //mov ebx,eax
                }
                
                累加32 ++ //inc eax
            }
            
            基数32 ++  //inc ebx
            [字节|基数32] = 0 //把最后一个左斜杠的后一个字节设为0
            字符串联接(取地址 ini文件路径名,取地址 ini文件名) //lstrcat
        }
    }
    
    //读取ini配置文件
    函数  读取ini文件(双字 h选项对话框句柄)
    {
        获得INI文件路径名()
        
        获取对话框子项(h选项对话框句柄,IDC_TREE1)//获取树控件
        读取树节(取地址 ini文件路径名,累加32)
        
        获取对话框子项(h选项对话框句柄,IDC_COMBO1)
        读取组合框节(取地址 ini文件路径名,累加32)
        
        获取对话框子项(h选项对话框句柄,IDC_LIST1)
        读取列表节(取地址 ini文件路径名,累加32)
        
        读取其它选项(取地址 ini文件路径名,h选项对话框句柄)
    }
    
    函数  读取树节(双字 ini文件路径,双字 h树控件句柄)
    {        
        ∥得到图像列表
        双字 h图像列表,h位图句柄
        图像列表_创建(16, 16, ILC_COLOR16, 5, 10)
        h图像列表 = 累加32
        加载位图(模块句柄, IDB_TREE)
        h位图句柄 = 累加32
        图像列表_添加(h图像列表, h位图句柄, NULL)
        删除对象(h位图句柄)
            
        ∥如果你不想让树控件项目有图标显示就注释到下一行
        发送消息(h树控件句柄, TVM_SETIMAGELIST, 0, h图像列表)
        
        读取树子结点(ini文件路径,h树控件句柄,取地址 树节名,0,TVI_ROOT)
    }
    
    函数  读取树子结点(双字 ini文件路径,双字 h树控件句柄,双字 子结点名,双字 h子结点句柄,双字 插入标志)
    {<源址32> //use esi
        
        字节 节缓冲区..256
        累加32 @= 节缓冲区 //lea eax,节缓冲区
        填充缓冲区(累加32,256,0) //将缓冲区清0
        累加32 @= 节缓冲区
        获取私有INI文件节(子结点名,累加32,256,ini文件路径)//GetPrivateProfileSection
        
        源址32 @= 节缓冲区        
        循环([字节|源址32] != 0)
        {   
            树视图_插入结构  t树插入
            双字 h临时结点
                
            压栈 h子结点句柄
            出栈 t树插入.h父结点
            压栈 插入标志
            出栈 t树插入.h在结点之后
            t树插入.项类型.项.掩码 = TVIF_TEXT | TVIF_IMAGE | TVIF_SELECTEDIMAGE
            t树插入.项类型.项.文本指针 = 源址32
            t树插入.项类型.项.图像序号 = 0
            t树插入.项类型.项.选中时图像 = 1
            累加32 @= t树插入
            发送消息(h树控件句柄, TVM_INSERTITEM, 0,累加32)//插入结点
            h临时结点 = 累加32
            
            //使用嵌套读取本结点的子结点数据
            读取树子结点(ini文件路径,h树控件句柄,源址32,h临时结点,TVI_LAST)            
            
            //展开子结点
            发送消息(h树控件句柄,TVM_EXPAND,TVE_EXPAND,h临时结点)
            
            //将字符串地址移到下一个字符串的位置
            字符串长度(源址32) 
            源址32 += 累加32
            源址32 ++
        }
    }
    
    函数  读取组合框节(双字 ini文件路径,双字 h组合框句柄)
    {<源址32>
                
        //读取组合框        
        字节 子项缓冲区..256        
        
        累加32 @= 子项缓冲区
        填充缓冲区(累加32,256,0) //将缓冲区清0
        累加32 @= 子项缓冲区
        获取私有INI文件节(取地址 组合框节名,累加32,256,ini文件路径)
        
        源址32 @= 子项缓冲区
        循环([字节|源址32] != 0)
        {            
            发送消息(h组合框句柄,组合框_ADDSTRING,0,源址32)//SendMessage
            
            字符串长度(源址32) //lstrlen
            源址32 += 累加32 //add esi,eax
            源址32 ++  //inc eax
        }
    }
    
    函数  读取列表节(双字 ini文件路径,双字 h列表控件)
    {<的址32,源址32>
                
        //添加列表列
        列表视图_列 l列表
        l列表.掩码 = LVCF_TEXT+LVCF_WIDTH
        l列表.文本指针 = 取地址 列表框列1
        l列表.宽度 = 50
        累加32 @= l列表
        发送消息(h列表控件, LVM_INSERTCOLUMN,0,累加32)
        l列表.文本指针 = 取地址 列表框列2
        累加32 @= l列表
        发送消息(h列表控件, LVM_INSERTCOLUMN, 1 ,累加32 )
        l列表.文本指针 = 取地址 列表框列3
        累加32 @= l列表
        发送消息(h列表控件, LVM_INSERTCOLUMN, 2 ,累加32)
            
        //读取列表项        
        字节 子项节缓冲区..256        
        累加32 @= 子项节缓冲区
        填充缓冲区(累加32,256,0) //将缓冲区清0
        累加32 @= 子项节缓冲区
        获取私有INI文件节(取地址 列表节名,累加32,256,ini文件路径)
        源址32 @= 子项节缓冲区        

        循环([字节|源址32] != 0)
        {            
            //取字符串以逗号分隔三份
            字节 姓名..20
            字节 姓别..10
            字节 年龄..10
            双字 循环次数
            
            压栈 源址32 //对源址寄存器进行保护
            
            的址32 @= 姓名
            循环次数 = 0            
            循环(循环次数 < 20)
            {
                累加低 串等于 [源址32] //运行完这条指令源址寄存器值会自动加1
                如果(累加低 == ',')//找到了分隔符
                {
                    累加低 = 0 //把分隔符设为字符串结束值
                    [的址32] 串等于 累加低 //运行完这条指令的址寄存器值会自动加1
                    跳出
                }
                [的址32] 串等于 累加低
                
                循环次数 ++
            }
            
            的址32 @= 姓别
            循环次数 = 0            
            循环(循环次数 < 10)
            {
                累加低 串等于 [源址32]
                如果(累加低 == ',')
                {
                    累加低 = 0
                    [的址32] 串等于 累加低                           
                    跳出
                }
                [的址32] 串等于 累加低
                
                循环次数 ++
            }
            
            的址32 @= 年龄
            循环次数 = 0            
            循环(循环次数 < 10)
            {
                累加低 串等于 [源址32]
                如果(累加低 == ',')
                {
                    累加低 = 0
                    [的址32] 串等于 累加低                                    
                    跳出
                }
                [的址32] 串等于 累加低
                
                循环次数 ++
            }                       
            
            列表视图_项 l列表项 
            //插入第一行
            l列表项.掩码 = LVIF_TEXT
            l列表项.项序号 = 0
            l列表项.子项序号 = 0       
            累加32 @= 姓名
            l列表项.文本指针 = 累加32
            累加32 @= l列表项
            发送消息(h列表控件, LVM_INSERTITEM,0,累加32)
        
            l列表项.子项序号 ++
            累加32 @= 姓别
            l列表项.文本指针 = 累加32
            累加32 @= l列表项
            发送消息(h列表控件,LVM_SETITEM, 0,累加32)
            
            l列表项.子项序号 ++
            累加32 @= 年龄
            l列表项.文本指针 = 累加32
            累加32 @= l列表项
            发送消息(h列表控件,LVM_SETITEM, 0,累加32)
                
            出栈 源址32
            字符串长度(源址32)
            源址32 += 累加32
            源址32 ++
        }
        
        //选择整行风格
        发送消息(h列表控件,LVM_SETEXTENDEDLISTVIEWSTYLE,LVS_EX_FULLROWSELECT,LVS_EX_FULLROWSELECT)
    }   
    
    函数  读取其它选项(双字 ini文件路径,双字 h选项对话框句柄)
    {     
        字节 编辑框字符串..100
        累加32 @= 编辑框字符串
        获取私有INI文件字符串(取地址 其它选项名,取地址 编辑框节名,0,累加32,256,ini文件路径) //GetPrivateProfileString
        累加32 @= 编辑框字符串
        设置对话框子项文本(h选项对话框句柄,IDC_EDIT1,累加32) //SetDlgItemText
        
        双字 最前标识
        获取私有INI文件整数(取地址 其它选项名,取地址 最前标识名,0,ini文件路径) //GetPrivateProfileInt
        最前标识 = 累加32
        如果(最前标识 > 0)
        {
            发送对话框子项消息(h选项对话框句柄,IDC_CHECKBOX3,BM_SETCHECK,真,0) //SendDlgItemMessage
            设置窗体位置(h选项对话框句柄,HWND_TOPMOST,0,0,0,0,SWP_NOMOVE | SWP_NOSIZE) //SetWindowPos
        }        
        
        //读取全选标识
        双字 全选标识
        获取私有INI文件整数(取地址 其它选项名,取地址 列表全选标识,0,ini文件路径)
        全选标识 = 累加32
        获取对话框子项(h选项对话框句柄,IDC_LIST1)
        列表控件全选(累加32,全选标识) //添加了列表控件全选函数
        发送对话框子项消息(h选项对话框句柄,IDC_CHECKBOX1,BM_SETCHECK,全选标识,0)
        
        双字 开始标识
        获取私有INI文件整数(取地址 其它选项名,取地址 开始标识名,0,ini文件路径)
        开始标识 = 累加32    
        如果(开始标识 > 0)
        {
            发送对话框子项消息(h选项对话框句柄,IDC_CHECKBOX2,BM_SETCHECK,真,0)            
            设置定时器(h选项对话框句柄,定时器事件编号,100,0)
            定时器ID = 累加32
        }   
    }
    
    ////////////////////////////////////////////////////////////
    
    //写入ini配置文件
    函数  写入ini文件(双字 h选项对话框句柄)
    {
        获得INI文件路径名()
        
        获取对话框子项(h选项对话框句柄,IDC_TREE1)//获取树控件
        写入树节(取地址 ini文件路径名,累加32)
        
        获取对话框子项(h选项对话框句柄,IDC_COMBO1)
        写入组合框节(取地址 ini文件路径名,累加32)
        
        获取对话框子项(h选项对话框句柄,IDC_LIST1)
        写入列表节(取地址 ini文件路径名,累加32)
        
        写入其它选项(取地址 ini文件路径名,h选项对话框句柄)
    }
    
    函数  写入树节(双字 ini文件路径,双字 h树控件句柄)
    {<的址32>
        
        //删除原来的结点
        写入私有INI文件节(取地址 树节名,0,ini文件路径)
        
        //取得根结点
        双字 h结点
        发送消息(h树控件句柄,TVM_GETNEXTITEM,TVGN_ROOT,0)
        h结点 = 累加32
        
        字节 树节点名称..256
        的址32 @= 树节点名称
        循环(h结点 != 0)
        {
            写入树结点(ini文件路径,h树控件句柄,h结点,的址32)
            字符串长度(的址32) //lstrlen
            的址32 += 累加32 //add edi,eax
            的址32 ++ //inc edi
            [字节|的址32] = 0 //mov ptr byte [edi],0
            
            发送消息(h树控件句柄,TVM_GETNEXTITEM,TVGN_NEXT,h结点)//得到下一个结点
            h结点 = 累加32
        }
        
        如果(树节点名称:0 != 0)
        {
            累加32 @= 树节点名称
            写入私有INI文件节(取地址 树节名,累加32,ini文件路径)
        }        
    }
    
    函数 写入树结点(双字 ini文件路径,双字 h树控件句柄,双字 h结点句柄,双字 结点名称缓冲区)
    {<的址32>
        
        TVITEM 树子项
        压栈 h结点句柄
        出栈 树子项.项句柄
        树子项.掩码 = TVIF_TEXT | TVIF_HANDLE        
        压栈 结点名称缓冲区
        出栈 树子项.文本指针
        树子项.文本最大长度 = 64
        累加32 @= 树子项
        发送消息(h树控件句柄,TVM_GETITEM,0,累加32)
                
        双字 h子结点
        发送消息(h树控件句柄,TVM_GETNEXTITEM,TVGN_CHILD,h结点句柄)//得到第一个子结点
        h子结点 = 累加32
        如果(h子结点 == 0) //如果没有子结点就返回
        {
            返回
        }
                
        //清除原有节的所有数据
        写入私有INI文件节(结点名称缓冲区,0,ini文件路径) //WritePrivateProfileSection
        
        字节 文本缓冲区..256
        的址32 @= 文本缓冲区
        循环(h子结点 != 0)//如果结点存在
        {
            字节 子结点名称..64
            累加32 @= 子结点名称
            写入树结点(ini文件路径,h树控件句柄,h子结点,累加32)//递归操作
            
            压栈 h子结点
            出栈 树子项.项句柄
            树子项.掩码 = TVIF_TEXT | TVIF_HANDLE            
            树子项.文本指针 = 的址32
            树子项.文本最大长度 = 64
            累加32 @= 树子项
            发送消息(h树控件句柄,TVM_GETITEM,0,累加32)            
            
            字符串长度(树子项.文本指针)
            的址32 += 累加32
            的址32 ++
            [字节|的址32] = 0 //设置最后的结束标志
            
             发送消息(h树控件句柄,TVM_GETNEXTITEM,TVGN_NEXT,h子结点)//得到下一个子结点
             h子结点 = 累加32
        }        
                
        累加32 @= 文本缓冲区
        写入私有INI文件节(结点名称缓冲区,累加32,ini文件路径) //WritePrivateProfileSection
    }
    
    函数  写入组合框节(双字 ini文件路径,双字 h组合框句柄)
    {<的址32>
        
        //删除原来的结点
        写入私有INI文件节(取地址 组合框节名,0,ini文件路径)
        
        双字 组合框项目数,当前项目序号
        发送消息(h组合框句柄,组合框_GETCOUNT,0,0)
        组合框项目数 = 累加32
        
        当前项目序号 = 0
        计数32 = 当前项目序号
        双字  文本缓冲区..256
        的址32 @= 文本缓冲区
        循环(计数32 < 组合框项目数)
        {
            发送消息(h组合框句柄,组合框_GETLBTEXT,计数32,的址32)
            字符串长度(的址32)
            的址32 += 累加32
            的址32 ++
            [字节|的址32] = 0
            
            当前项目序号 ++
            计数32 = 当前项目序号
        }
        
        如果(文本缓冲区:0 != 0)
        {
            累加32 @= 文本缓冲区
            写入私有INI文件节(取地址 组合框节名,累加32,ini文件路径)
        }
    }
    
    函数  写入列表节(双字 ini文件路径,双字 h列表控件)
    {<的址32>
        
        写入私有INI文件节(取地址 列表节名,0,ini文件路径)
        
        双字 列表项目数,当前项目序号
        发送消息(h列表控件,LVM_GETITEMCOUNT,0,0)        
        列表项目数 = 累加32
        
        当前项目序号 = 0
        计数32 = 当前项目序号
        双字  文本缓冲区..256
        的址32 @= 文本缓冲区
        循环(计数32 < 列表项目数)
        {
            列表视图_项 l列表项 
            
            //得到姓名
            l列表项.文本指针 = 的址32
            l列表项.文本最大长度 = 64
            l列表项.子项序号 = 0
            累加32 @= l列表项
            发送消息(h列表控件,LVM_GETITEMTEXT,当前项目序号,累加32)
            字符串长度(的址32)
            的址32 += 累加32            
            [字节|的址32] = ','
            的址32 ++
            
            //得到姓别
            l列表项.文本指针 = 的址32
            l列表项.文本最大长度 = 64
            l列表项.子项序号 = 1            
            累加32 @= l列表项
            发送消息(h列表控件,LVM_GETITEMTEXT,当前项目序号,累加32)
            字符串长度(的址32)
            的址32 += 累加32            
            [字节|的址32] = ','
            的址32 ++
            
            //得到年龄
            l列表项.文本指针 = 的址32
            l列表项.文本最大长度 = 64
            l列表项.子项序号 = 2
            累加32 @= l列表项
            发送消息(h列表控件,LVM_GETITEMTEXT,当前项目序号,累加32)
            字符串长度(的址32)
            的址32 += 累加32
            的址32 ++
            [字节|的址32] = 0
            
            当前项目序号 ++
            计数32 = 当前项目序号
        }    
        
        如果(文本缓冲区:0 != 0)
        {
            累加32 @= 文本缓冲区
            写入私有INI文件节(取地址 列表节名,累加32,ini文件路径)
        }    
    }
    
    函数  写入其它选项(双字 ini文件路径,双字 h选项对话框句柄)
    {
        字节 文本缓冲区..100
        累加32 @= 文本缓冲区
        获取对话框子项文本(h选项对话框句柄,IDC_EDIT1,累加32,100) //GetDlgItemText
        累加32 @= 文本缓冲区
        写入私有INI文件字符串(取地址 其它选项名,取地址 编辑框节名,累加32,ini文件路径) //WritePrivateProfileString   
        
        双字 最前标识
        最前标识 = 0
        对话框按钮被选中(h选项对话框句柄,IDC_CHECKBOX3) //检查按钮是否被选中
        如果(累加32 == BST_CHECKED) //被选中
        {
            最前标识 = 1
        }
        累加32 @= 文本缓冲区
        字符串格式化(累加32,取地址 格式化缓存,最前标识) //wsprintf
        累加32 @= 文本缓冲区
        写入私有INI文件字符串(取地址 其它选项名,取地址 最前标识名,累加32,ini文件路径)
        
        //全选标识
        双字 全选标识
        全选标识 = 0
        对话框按钮被选中(h选项对话框句柄,IDC_CHECKBOX1) //检查按钮是否被选中
        如果(累加32 == BST_CHECKED) //被选中
        {
            全选标识 = 1
        }
        累加32 @= 文本缓冲区
        字符串格式化(累加32,取地址 格式化缓存,全选标识) //wsprintf
        累加32 @= 文本缓冲区
        写入私有INI文件字符串(取地址 其它选项名,取地址 列表全选标识,累加32,ini文件路径)
        
        双字 开始标识
        开始标识 = 0
        对话框按钮被选中(h选项对话框句柄,IDC_CHECKBOX2) //检查按钮是否被选中
        如果(累加32 == BST_CHECKED) //被选中
        {
            开始标识 = 1
        }
        累加32 @= 文本缓冲区
        字符串格式化(累加32,取地址 格式化缓存,开始标识) //wsprintf
        累加32 @= 文本缓冲区
        写入私有INI文件字符串(取地址 其它选项名,取地址 开始标识名,累加32,ini文件路径)        
    }
}