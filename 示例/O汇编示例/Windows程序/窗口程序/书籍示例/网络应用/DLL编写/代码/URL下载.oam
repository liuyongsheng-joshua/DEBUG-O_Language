//O汇编文件

.包含文<*oasm32.oah>
.包含文<*.\中文视窗.oah>
.包含文<*.\user32.oah>
.包含文<*.\Kernel32.oah>
.包含文<*.\comdlg32.oah>
.包含文<..\..\网络应用\资源\资源.oah>
.包含文<*.\wininet.oah>
.包含文<*.\masm32.oah>

.引用库<*.\user32.lib>
.引用库<*.\kernel32.lib>
.引用库<*.\comdlg32.lib>
.引用库<*.\wininet.lib>
.引用库<*.\masm32.lib>


.预留段
{
    双字 h对话框句柄
    双字 h下载线程句柄
    字节 文件名缓冲区..256
    字节 URL地址缓冲区..512
}

.只读段
{
    字节 HTTP标识符..         = "http:\//" //字符串中的左双斜杠前必须用转义字符'\'
    字节 打开文件对话框过滤.. = "所有文件(*.*)","*.*",0    
}

.代码段
{
    函数 创建URL下载窗口:CreateURLDownloadWnd(双字 h对话框句柄)
    {
        获取模块句柄(0)
        创建对话框参数(累加32,IDD_URL_DOWNLOAD,h对话框句柄,URL下载话框过程,0)
    }
    
    函数 URL下载话框过程(双字 句柄,双字 消息,双字 消息参数一,双字 消息参数二)
    {<基数32,的址32,源址32>
        
        累加32 = 消息
        如果(累加32 == 消息_INITDIALOG) //对话框初始化消息
        {              
            压栈 句柄
            出栈 h对话框句柄
        }
        否则如果(累加32 == 消息_CLOSE) //关闭消息
        {   
            如果(h下载线程句柄 != 0)
            {
                终止线程(h下载线程句柄,-1) //TerminateThread
                h下载线程句柄 = 0
            }
            终止对话框(句柄,0)
        }        
        否则如果(累加32 == 消息_COMMAND) //如果是命令消息
        {
            累加32 = 消息参数一
            如果(累加16 == IDC_BUTTON_DOWNLOAD) //如果是"开始下载"按钮
            {
                开始下载()                                                     
            }            
        }
        否则
        {
            返回  假
        }        
        
        返回  真
    }
    
    函数 获取要保存的文件名(双字 h父窗口句柄,双字 lp标题,双字 lp扩展名过滤)
    {
        打开文件名结构 文件对话框结构
        文件对话框结构.lStructSize = 取大小 打开文件名结构
        压栈 h父窗口句柄
        出栈 文件对话框结构.hWndOwner
        获取模块句柄(0)
        文件对话框结构.hInstance = 累加32
        压栈 lp扩展名过滤
        出栈 文件对话框结构.lpstrFilter        
        文件对话框结构.lpstrFile =   取地址 文件名缓冲区
        文件对话框结构.nMaxFile  =   取大小 文件名缓冲区
        压栈 lp标题
        出栈 文件对话框结构.lpstrTitle
        文件对话框结构.Flags =              OFN_EXPLORER | OFN_LONGNAMES

        获取保存文件名(取地址 文件对话框结构) //GetSaveFileName
    }
        
    函数 设置提示信息(双字 lp信息文本)
    {
        设置对话框子项文本(h对话框句柄,IDC_STATIC_INFO,lp信息文本) //SetDlgItemText
    }
    
    函数 处理URL地址()
    {
        获取对话框子项文本(h对话框句柄,IDC_EDIT_URL,取地址 URL地址缓冲区,取大小 URL地址缓冲区) //GetDlgItemText
        如果(URL地址缓冲区:0 == 0)
        {
            设置提示信息(取地址 "请输入URL地址")
            返回 假
        }
        
        字节 URL临时缓冲区..512
        字符串复制(&URL临时缓冲区,取地址 URL地址缓冲区) //lstrcpy
        URL临时缓冲区:7 = 0
        
        字符串比较无大小写(&URL临时缓冲区,取地址 HTTP标识符) //lstrcmpi
        如果(累加32 != 0) //字符串不匹配
        {
            字符串复制(&URL临时缓冲区,取地址 URL地址缓冲区) //lstrcpy
            字符串复制(取地址 URL地址缓冲区,取地址 HTTP标识符) //lstrcpy
            字符串联接(取地址 URL地址缓冲区,&URL临时缓冲区) //lstrcat
            设置对话框子项文本(h对话框句柄,IDC_EDIT_URL,取地址 URL地址缓冲区) //SetDlgItemText
        }
        
        返回 真
    }
    
    函数 下载URL文件()
    {<基数32,计数32,数据32,源址32,的址32>
        
        //打开互联网
        互联网打开(取地址 "下载URL文件示例",INTERNET_OPEN_TYPE_PRECONFIG,0,0,0) //InternetOpen
        如果(累加32 == 0)
        {
            设置提示信息(取地址 "打开互联网错误,请确认是否已经连接到互联网!")
            返回
        }
        双字 h互联网会话句柄
        h互联网会话句柄 = 累加32
        
        //打开URL
        设置提示信息(取地址 "正在连接主机...")
        互联网打开Url(h互联网会话句柄,取地址 URL地址缓冲区,0,0,INTERNET_FLAG_NO_AUTO_REDIRECT,0) //InternetOpenUrl
        如果(累加32 == 0)
        {
            设置提示信息(取地址 "无法连接到主机,请重新输入URL地址")
            跳转到 错误一
        }
        双字 hURL文件句柄
        hURL文件句柄 = 累加32
        
        字节 临时缓冲区..1024
        双字 n长度
        //检测文件是否存在
        n长度 = 取大小 临时缓冲区
        Http询问信息(hURL文件句柄,HTTP_QUERY_STATUS_CODE,&临时缓冲区,&n长度,0) //HttpQueryInfo
        如果(累加32 == 0)
        {
            设置提示信息(取地址 "检测文件是否存在时,接收数据错误")
            跳转到 错误二
        }
        
        字符串比较(&临时缓冲区,取地址 "200")
        如果(累加32 != 0)
        {
            设置提示信息(取地址 "URL文件不存在,请重新输入URL文件地址")
            跳转到 错误二
        }
        
        //检测文件长度        
        Http询问信息(hURL文件句柄,HTTP_QUERY_CONTENT_LENGTH,&临时缓冲区,&n长度,0) //HttpQueryInfo
        如果(累加32 != 0)
        {
            //设置提示信息(取地址 "获取文件长度时,接收数据错误")
            //跳转到 错误二
            
            字符到长整数(&临时缓冲区) //将字符转换成整数        
            双字 n文件长度
            n文件长度 = 累加32
        }        
        
        //建立存盘文件
        获取要保存的文件名(h对话框句柄,取地址 "保存URL文件",取地址 打开文件对话框过滤)
        如果(文件名缓冲区:0 == 0) //如果没有得到文件名
        {
            设置提示信息(取地址 "没有得到文件名")
            跳转到 错误二
        }
        创建文件(取地址 文件名缓冲区,GENERIC_READ | GENERIC_WRITE,FILE_SHARE_READ,0,CREATE_ALWAYS,FILE_ATTRIBUTE_NORMAL,0) //CreateFile
        如果(累加32 == INVALID_HANDLE_VALUE)
        {
            设置提示信息(取地址 "创建文件失败")
            跳转到 错误二
        }
        双字 h保存文件句柄
        h保存文件句柄 = 累加32
        
        设置提示信息(取地址 "正在下载文件...")
        
        获取对话框子项(h对话框句柄,IDC_BUTTON_DOWNLOAD)
        有效窗体(累加32,假)
                
        //开始下载文件数据
        循环(真)
        {
            互联网读文件(hURL文件句柄,&临时缓冲区,取大小 临时缓冲区,&n长度) //InternetReadFile
            如果(累加32 == 0)
            {
                 设置提示信息(取地址 "读取文件错误")
                 跳出
            }
            
            如果(n长度 == 0)
            {
                设置提示信息(取地址 "文件下载结束")
                
                获取对话框子项(h对话框句柄,IDC_BUTTON_DOWNLOAD)
                有效窗体(累加32,真)
        
                跳出
            }
            
            双字 n写入长度
            写文件(h保存文件句柄,&临时缓冲区,n长度,&n写入长度,0) //WriteFile
        }
        
        关闭句柄(h保存文件句柄) //CloseHandle
        
   标签 错误二:
        互联网关闭句柄(hURL文件句柄) //InternetCloseHandle
        
   标签 错误一:
        互联网关闭句柄(h互联网会话句柄) //InternetCloseHandle
        
        下载结束()
    }
    
    函数 开始下载()
    {
        处理URL地址()
        如果(累加32 == 假)
        {
            返回
        }    
                
        h下载线程句柄 = 0
        创建线程(0,0,下载URL文件,0,0,取地址 h下载线程句柄) //CreateThread
    }
    
    函数 下载结束()
    {
        如果(h下载线程句柄 != 0)
        {
            关闭句柄(h下载线程句柄) //CloseHandle
            h下载线程句柄 = 0
        }
    }
}