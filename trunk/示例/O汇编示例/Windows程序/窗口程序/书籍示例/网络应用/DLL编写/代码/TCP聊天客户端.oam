//O汇编文件

.包含文<*oasm32.oah>
.包含文<*.\中文视窗.oah>
.包含文<*.\user32.oah>
.包含文<*.\Kernel32.oah>
.包含文<..\..\网络应用\资源\资源.oah>
.包含文<*.\wsock32.oah>

.引用库<*.\user32.lib>
.引用库<*.\kernel32.lib>
.引用库<*.\wsock32.lib>

宏定义
{
    自消息_SOCK             消息_USER+0x100
    自消息_GETHOSTINFO      消息_USER+0x101
    默认端口        7390
}

枚举 发送命令
{
    命令_昵称
    命令_用户列表
    命令_信息
}

.预留段
{   
    字节 服务器地址..100
    字节 昵称..50
    双字 主机缓冲区..MAXGETHOSTSTRUCT
    字节 接收内容缓冲区..1024
    字节 发送内容缓冲区..1024
}

.代码段
{
    函数 创建TCP聊天客户端:CreateTCPClient(双字 h父窗口句柄)
    {
        获取模块句柄(0)
        创建对话框参数(累加32,IDD_TCP_CHAT,h父窗口句柄,TCP聊天对话框过程,0)
    }
    
    函数 TCP聊天对话框过程(双字 句柄,双字 消息,双字 消息参数一,双字 消息参数二)
    {<基数32,的址32,源址32>
        
        累加32 = 消息
        如果(累加32 == 消息_INITDIALOG) //对话框初始化消息
        {   
            WSADATA wsa数据
            WSA启动(0x101,&wsa数据) //WSAStartup
            如果(累加32 != 0)
            {
                提示框(句柄,取地址 "初始化套接字动态链接库错误!",取地址 "错误提示",信息框_OK)
            }
        }
        否则如果(累加32 == 消息_CLOSE) //关闭消息
        {            
            获取套接字(句柄)
            关闭套接字(累加32) //closesocket
            WSA清除() //WSACleanup
            终止对话框(句柄,0)
        }
        否则如果(累加32 == 自消息_GETHOSTINFO)
        {
            累加32 = 消息参数二
            累加32 逻辑右移 16
            如果(累加16 != 0)
            {
                提示框(句柄,取地址 "输入的服务器域名错误!",取地址 "登录错误",信息框_OK)
            }
            否则
            {
                连接服务器(句柄)
                如果(累加32 == 真)
                {
                    获取对话框子项(句柄,IDC_BUTTON_LOGIN)
                    有效窗体(累加32,假)
                }
            }
        }
        否则如果(累加32 == 自消息_SOCK)
        {            
            累加32 = 消息参数二
            如果(累加16 == FD_READ)
            {
                接收信息(句柄,消息参数一)
            }            
            否则如果(累加16 == FD_CONNECT)
            {
                累加32 = 消息参数二
                累加32 逻辑右移 16
                如果(累加16 != 0)
                {
                    关闭套接字(消息参数一)  //closesocket
                    提示框(句柄,取地址 "连接服务器失败!",取地址 "连接提示",信息框_OK)
                    获取对话框子项(句柄,IDC_BUTTON_LOGIN)
                    有效窗体(累加32,真)
                    获取对话框子项(句柄,IDC_BUTTON_SEND)
                    有效窗体(累加32,假)
                }
                否则
                {
                    //提示框(句柄,取地址 "连接服务器成功!",取地址 "连接提示",信息框_OK)
                    获取对话框子项(句柄,IDC_BUTTON_LOGIN)
                    有效窗体(累加32,假)
                    获取对话框子项(句柄,IDC_BUTTON_SEND)
                    有效窗体(累加32,真)
                    双字 发送内容长度
                    发送内容长度 = 0
                    累加32 = 取地址 发送内容缓冲区
                    压栈 命令_昵称
                    出栈 [双字|累加32]
                    发送内容长度 = 取大小 命令_昵称
                    累加32 += 发送内容长度
                    字符串复制(累加32,取地址 昵称)
                    字符串长度(取地址 昵称)
                    发送内容长度 += 累加32
                    发送(消息参数一,取地址 发送内容缓冲区,发送内容长度,0) //send
                }
            }
            否则如果(累加16 == FD_CLOSE)
            {
                关闭套接字(消息参数一)  //closesocket
                
                //删除列表框所有项
                发送对话框子项消息(句柄,IDC_LIST_MEMBER,列表框_GETCOUNT,0,0)
                循环(累加32 > 0)
                {
                    发送对话框子项消息(句柄,IDC_LIST_MEMBER,列表框_DELETESTRING,0,0)//删除第一项
                    发送对话框子项消息(句柄,IDC_LIST_MEMBER,列表框_GETCOUNT,0,0)
                }
                
                //删除私聊列表框所有项
                发送对话框子项消息(句柄,IDC_LIST_TALKTO,列表框_GETCOUNT,0,0)
                循环(累加32 > 0)
                {
                    发送对话框子项消息(句柄,IDC_LIST_TALKTO,列表框_DELETESTRING,0,0)//删除第一项
                    发送对话框子项消息(句柄,IDC_LIST_TALKTO,列表框_GETCOUNT,0,0)
                }
                
                获取对话框子项(句柄,IDC_BUTTON_LOGIN)
                有效窗体(累加32,真)
                获取对话框子项(句柄,IDC_BUTTON_SEND)
                有效窗体(累加32,假)
            }
        }    
        否则如果(累加32 == 消息_COMMAND) //如果是命令消息
        {
            累加32 = 消息参数一
            如果(累加16 == IDC_BUTTON_LOGIN) //如果是"登录"按钮
            {
                登录服务器(句柄)
            }
            如果(累加16 == IDC_BUTTON_SEND) //如果是"发送"按钮
            {
                发送说的话(句柄)
            }   
            否则如果(累加16 == IDC_LIST_MEMBER)//用户列表框
            {
                累加32 = 消息参数一
                累加32 逻辑右移 16
                如果(累加16 == LBN_DBLCLK)
                {
                    字节 用户昵称..50
                    双字 n当前项,n新增项
                    双字 h用户套接字
                    发送对话框子项消息(句柄,IDC_LIST_MEMBER,列表框_GETCURSEL,0,0)                    
                    n当前项 = 累加32
                    发送对话框子项消息(句柄,IDC_LIST_MEMBER,列表框_GETITEMDATA,n当前项,0)
                    h用户套接字 = 累加32
                    
                    //检测用户是否已经在私聊列表中                    
                    双字 n项序号,n私聊用户个数                                        
                    发送对话框子项消息(句柄,IDC_LIST_TALKTO,列表框_GETCOUNT,0,0)
                    n私聊用户个数 = 累加32
                    n项序号 = 0
                    计数32 = n项序号
                    循环(计数32 < n私聊用户个数)
                    {
                        发送对话框子项消息(句柄,IDC_LIST_TALKTO,列表框_GETITEMDATA,计数32,0)
                        如果(累加32 == h用户套接字)
                        {
                            返回 真
                        }
                        
                        n项序号 ++
                        计数32 = n项序号
                    }
                    
                    发送对话框子项消息(句柄,IDC_LIST_MEMBER,列表框_GETTEXT,n当前项,&用户昵称)
                    发送对话框子项消息(句柄,IDC_LIST_TALKTO,列表框_ADDSTRING,0,&用户昵称)
                    n新增项 = 累加32
                    发送对话框子项消息(句柄,IDC_LIST_TALKTO,列表框_SETITEMDATA,n新增项,h用户套接字)
                }
            }
            否则如果(累加16 == IDC_LIST_TALKTO)//私聊用户列表框
            {
                累加32 = 消息参数一
                累加32 逻辑右移 16
                如果(累加16 == LBN_DBLCLK)//双击私聊用户项则删除该项
                {
                    发送对话框子项消息(句柄,IDC_LIST_TALKTO,列表框_GETCURSEL,0,0)
                    发送对话框子项消息(句柄,IDC_LIST_TALKTO,列表框_DELETESTRING,累加32,0)
                }
            }
        }
        否则
        {
            返回  假
        }        
        
        返回  真
    }
    
    函数 登录服务器(双字 h对话框句柄)
    {
        获取对话框子项文本(h对话框句柄,IDC_EDIT_SERVER_ADDR,取地址 服务器地址,100)
        如果(服务器地址:0 == 0)//如果字符串为空
        {
            提示框(h对话框句柄,取地址 "服务器地址不能为空",取地址 "登录提示",信息框_OK)
            返回
        }
        
        获取对话框子项文本(h对话框句柄,IDC_EDIT_NICKNAME,取地址 昵称,50)
        如果(昵称:0 == 0)//如果字符串为空
        {
            提示框(h对话框句柄,取地址 "昵称不能为空",取地址 "登录提示",信息框_OK)
            返回
        }
        
        WSA异步获取主机别名(h对话框句柄,自消息_GETHOSTINFO,取地址 服务器地址,取地址 主机缓冲区,MAXGETHOSTSTRUCT) //WSAAsyncGetHostByName
    }
    
    函数 连接服务器(双字 h对话框句柄)
    {        
        套接字(AF_INET,SOCK_STREAM,0) //socket
        如果(累加32==INVALID_SOCKET)
        {
            提示框(h对话框句柄,取地址 "创建套接字错误!",取地址 "连接提示",信息框_OK)
            返回 假
        } 
       双字 h套接字      
       h套接字 = 累加32
       WSA异步选择(h套接字,h对话框句柄,自消息_SOCK,FD_CONNECT | FD_WRITE | FD_READ | FD_CLOSE) //WSAAsyncSelect
       如果(累加32==SOCKET_ERROR)
       {
           WSA获取最后错误() //WSAGetLastError
           如果(累加32!=WSAEWOULDBLOCK)
           {
               关闭套接字(h套接字)  //closesocket
               提示框(h对话框句柄,取地址 "异步选择模式错误!",取地址 "连接提示",信息框_OK)
               返回 假
           }             
       }
       主机到网络短整数(默认端口) //htons
       sockaddr_in sin套接字
       sin套接字.sin_port = 累加16
       sin套接字.sin_family = AF_INET
       累加32 = 取地址 主机缓冲区
       累加32 = [双字|累加32 + &hostent.h_list]
       累加32 = [累加32]
       累加32 = [累加32]
       sin套接字.sin_addr = 累加32
       连接(h套接字,&sin套接字,取大小 sin套接字) //connect
       
       保存套接字(h对话框句柄,h套接字)
       
       返回 真
    }
    
    函数 保存套接字(双字 h主窗口句柄,双字 h套接字)
    {
        设置对话框子项整数值(h主窗口句柄,IDC_STATIC_SOCKET,h套接字,假)
    }
    
    函数 获取套接字(双字 h主窗口句柄)
    {
        获取对话框子项整数值(h主窗口句柄,IDC_STATIC_SOCKET,0,假)
    }
    
    函数 接收信息(双字 h主窗口句柄,双字 h当前套接字)
    {
        Rtl内存清零(取地址 接收内容缓冲区,1024) //RtlZeroMemory
        接收(h当前套接字,取地址 接收内容缓冲区,1024,0) //recv
        如果(累加32 > 0) //接收到的字节数
        {
            双字 接收长度
            接收长度 = 累加32
            
            基数32 = 取地址 接收内容缓冲区
            累加32 = [双字|基数32]            
            如果(累加32 == 命令_用户列表)
            {
                基数32 += 取大小 命令_用户列表
                刷新用户列表(h主窗口句柄,h当前套接字,基数32)
            }
            否则如果(累加32 == 命令_信息)
            {
                基数32 += 取大小 命令_信息
                发送对话框子项消息(h主窗口句柄,IDC_EDIT_CHAT_CONTENT,EM_SETSEL,-1,-1)
                发送对话框子项消息(h主窗口句柄,IDC_EDIT_CHAT_CONTENT,EM_REPLACESEL,0,基数32)
            }
        }
    }
    
    函数 刷新用户列表(双字 h主窗口句柄,双字 h当前套接字,双字 用户列表地址)
    {<源址32>
                
        //删除列表框所有项
        发送对话框子项消息(h主窗口句柄,IDC_LIST_MEMBER,列表框_GETCOUNT,0,0)
        循环(累加32 > 0)
        {
            发送对话框子项消息(h主窗口句柄,IDC_LIST_MEMBER,列表框_DELETESTRING,0,0)//删除第一项
            发送对话框子项消息(h主窗口句柄,IDC_LIST_MEMBER,列表框_GETCOUNT,0,0)
        }
        
        双字 h用户套接字
        字节 用户昵称..50
        源址32 = 用户列表地址
        
        循环(真)
        {
            压栈 [双字|源址32]
            出栈 h用户套接字
            如果(h用户套接字 == 0)
            {
                跳出
            }
            源址32 += 取大小 h用户套接字
                        
             //添加用户信息
            发送对话框子项消息(h主窗口句柄,IDC_LIST_MEMBER,列表框_ADDSTRING,0,源址32)
            发送对话框子项消息(h主窗口句柄,IDC_LIST_MEMBER,列表框_SETITEMDATA,累加32,h用户套接字)
            
            字符串长度(源址32)
            源址32 += 累加32
            源址32 ++
        }
    }
    
    函数 发送说的话(双字 h主窗口句柄)
    {<源址32,的址32>
        
        字节 要说的话..512
        获取对话框子项文本(h主窗口句柄,IDC_EDIT_SAY,&要说的话,512)
        如果(要说的话:0 == 0)//如果字符串为空
        {
            提示框(h主窗口句柄,取地址 "请输入要发送的内容",取地址 "发送提示",信息框_OK)
            发送对话框子项消息(h主窗口句柄,IDC_EDIT_SAY,消息_SETFOCUS,0,0)
            返回
        }
                     
        双字 发送内容长度
        的址32 = 取地址 发送内容缓冲区
        压栈 命令_信息
        出栈 [双字|的址32]
        发送内容长度 = 取大小 命令_信息
        的址32 += 发送内容长度
        字符串复制(的址32,&要说的话)
        字符串长度(&要说的话)        
        发送内容长度 += 累加32
        发送内容长度 ++
        的址32 += 累加32
        的址32 ++
        
        对话框按钮被选中(h主窗口句柄,IDC_CHECKBOX_TALKTO) //检查按钮是否被选中
        如果(累加32 == BST_CHECKED)//选中了
        {
            双字 n用户个数
            发送对话框子项消息(h主窗口句柄,IDC_LIST_TALKTO,列表框_GETCOUNT,0,0)
            如果(累加32 == 0)
            {
                提示框(h主窗口句柄,取地址 "没有私聊用户,请添加私聊用户",取地址 "发送提示",信息框_OK)
                返回
            }
            n用户个数 = 累加32
                    
            双字 n当前项
            n当前项 = 0
            计数32 = n当前项
            源址32 = 的址32
            循环(计数32 < n用户个数)
            {
                发送对话框子项消息(h主窗口句柄,IDC_LIST_TALKTO,列表框_GETITEMDATA,计数32,0)
                [双字|的址32] = 累加32                
                的址32 += 取大小 双字
                发送内容长度 += 取大小 双字
            
                n当前项 ++
                计数32 = n当前项
            }
        }
                
        [双字|的址32] = 0
        发送内容长度 += 取大小 双字
                        
        获取套接字(h主窗口句柄)
        发送(累加32,取地址 发送内容缓冲区,发送内容长度,0) //send
    }
}