∥创建简单的窗口

.包含文<*oasm32.oah>
//包含下面三个文件
.包含文<*.\中文视窗.oah>
.包含文<*.\user32.oah>
.包含文<*.\Kernel32.oah>
.包含文<..\资源\资源.oah>
.包含文<*.\gdi32.oah>
.包含文<*.\comctl32.oah>


.引用库<*Kernel32.lib>
.引用库<*user32.lib>
.引用库<*.\gdi32.lib>
.引用库<*.\comctl32.lib>


.包含文<..\包含\工具栏.oam>
.包含文<..\包含\状态栏.oam>
.包含文<..\包含\编辑框.oam>



//删除原来的结构体定义

宏定义
{
    //删除原来的一些宏定义,只保留下面的宏定义
    默认坐标X 100
    默认坐标Y 80
    默认宽度  800
    默认高度  600
}



.只读段
{
    字节 窗口类名.. = "MyFirstWinClass"
    字节 窗口标题.. = "O汇编示例--使用窗口控件"  //把原来的窗口标题"程序主窗口"改为"使用窗口控件"
    字节 提醒内容.. = "主窗口创建失败"
    字节 提醒标题.. = "警告"
    字节 状态文本.. = "准备就绪"
    
    字节 信息框内容1.. = "点击了新建命令"
    字节 信息框内容2.. = "点击了打开命令"
    字节 信息框内容3.. = "点击了撤销菜单"
    字节 信息框内容4.. = "点击了重做菜单"
    字节 信息框内容5.. = "处理其它命令"
    字节 信息框标题..  = "提示"
}

//删除原来的引用段

.数据段
{
    双字  模块句柄
}

.代码段
{
    入口 主函数()
    {        
        获取模块句柄(0)
        模块句柄 = 累加32
        
        主窗口()
        退出进程(0)
    }
    
    函数 主窗口()
    {        
        窗体结构  我的窗口类
        
        我的窗口类.结构大小 = 取大小 我的窗口类
        我的窗口类.窗口风格 = 类风格_HREDRAW | 类风格_VREDRAW //把"垂直重绘 | 水平重绘"改成"类风格_HREDRAW | 类风格_VREDRAW"
        我的窗口类.窗口过程 = 消息处理程序
        我的窗口类.类名附加空间 = 0
        我的窗口类.窗口附加空间 = 0

        压栈  模块句柄
        出栈  我的窗口类.实例句柄
        
        获取固有对象(HOLLOW_BRUSH)
        我的窗口类.背景颜色 = 累加32     //把"背景色"改成"空的",以使改变窗口大小时不会闪烁
        //我的窗口类.背景颜色 = 颜色_WINDOW     //把"背景色"改成"颜色_WINDOW"
        我的窗口类.菜单指针 = 0     
        我的窗口类.类名指针 = 取地址  窗口类名
        
        加载图标(模块句柄,IDI_MAIN)
        //加载图标(0,图标ID_APPLICATION) //把原来的"载入图标"改成"加载图标"  "图标形状"改成 "图标ID_APPLICATION"
        我的窗口类.图标句柄 = 累加32
       
        我的窗口类.附加图标 = 累加32

        加载指针(0,指针ID_ARROW) //把原来的"载入光标"改成"加载指针"  "鼠标形状"改成"指针ID_ARROW"
        我的窗口类.光标句柄 = 累加32
        
        累加32 @= 我的窗口类
        高级注册窗体类(累加32) //"注册窗口类"改成"高级注册窗体类"

        双字  主窗口句柄
        //按下面的风格创建窗口
        高级创建窗体(窗体风格_EX_LEFT,取地址 窗口类名,取地址 窗口标题,窗体风格_OVERLAPPEDWINDOW,默认坐标X,默认坐标Y,默认宽度,默认高度,0,0,模块句柄,0)  //CreateWindowEx
        主窗口句柄 = 累加32
        如果(主窗口句柄 == 0)
        {
            提示框(0,取地址 提醒内容,取地址 提醒标题,16)  //把"信息窗口"改成"提示框"
            返回 0
        }
        
        加载菜单(模块句柄,IDR_MENU_MAIN)  ∥ 菜单 ID  LoadMenu
        设置菜单(主窗口句柄,累加32)  //SetMenu
                
        显示窗体(主窗口句柄,显示窗体_SHOWNORMAL)  //"显示窗口"改成 "显示窗体"  "显示方式"改成"显示窗体_SHOWNORMAL"
        更新窗体(主窗口句柄) //"更新窗口"改成 "更新窗体"
        
        消息结构  消息
        循环(真)
        {
             累加32 @= 消息
             获取消息(累加32,0,0,0)
             如果(累加32==0)
             {
                 跳出  //break
             }
             累加32 @= 消息
             消息翻译(累加32)
             累加32 @= 消息
             消息调度(累加32)
        }        
        
    }//主窗口函数结束
    
    函数 消息处理程序(双字 句柄,双字 消息,双字 消息参数一,双字 消息参数二)
    {<基数32,的址32,源址32>  //这里是对这些寄存器值进行保护的高级写法 
           
        累加32 = 消息
        如果(累加32 == 消息_CREATE) //创建窗口消息
        {
            构建工具条(句柄,IDB_TOOLBAR)  //构建工具条
            构建状态条(句柄)  //构建状态条
            构建编辑框(句柄)  //构建编辑框
            
            //设置第一个状态格文本为"准备就绪"
            发送消息(h状态栏句柄,状态条_SETTEXT,0,取地址 状态文本)
        }
        否则如果(累加32 == 消息_CLOSE)   //把"关闭消息"改成"消息_CLOSE"
        {            
            邮送退出消息(0)  //把"邮送结束消息"改成"邮送退出消息"
        }
        否则如果(累加32 == 消息_COMMAND) //响应命令消息
        {
            //新建菜单或者工具条第一个按钮
            如果((消息参数一 == IDM_NEW) || (消息参数一 == 50))
            {
                提示框(0,取地址 信息框内容1,取地址 信息框标题,信息框_OK)
            }
            //打开菜单或者工具条第二个按钮
            否则如果((消息参数一 == IDM_OPEN) || (消息参数一 == 51))
            {
                提示框(0,取地址 信息框内容2,取地址 信息框标题,信息框_OK)
            }
            //撤销菜单
            否则如果(消息参数一 == IDM_UNDO)
            {
                提示框(0,取地址 信息框内容3,取地址 信息框标题,信息框_OK)
            }
            //重做菜单
            否则如果(消息参数一 == IDM_REDO)
            {
                提示框(0,取地址 信息框内容4,取地址 信息框标题,信息框_OK)
            }
            //退出菜单 
            否则如果(消息参数一 == IDM_QUIT)
            {
                发送消息(句柄,消息_CLOSE,0,0) //发送关闭消息
            }                 
        }        
        否则如果(累加32 == 消息_SIZE)
        {
            发送消息(h工具条句柄,工具条_AUTOSIZE,0,0)
            移动窗体(h状态栏句柄,0,0,0,0,TRUE)
            
            矩形结构  r矩形
            双字  n工具条高度
            双字  n状态条高度
            双字  n窗口宽度
            双字  n窗口高度
            
            //得到工具条高度
            累加32 @= r矩形
            获取客户区矩形(h工具条句柄,累加32)
            压栈 r矩形.底  //push r矩形.底
            出栈 n工具条高度     ∥ 工具条高度 pop n工具条高度

            //得到状态条高度
            累加32 @= r矩形  //lea eax,r矩形
            获取客户区矩形(h状态栏句柄,累加32)
            压栈 r矩形.底
            出栈 n状态条高度     ∥ 状态栏高度
            
            //得到主窗口客户区宽度和高度
            累加32 @= r矩形
            获取客户区矩形(句柄,累加32)
            压栈 r矩形.右
            出栈 n窗口宽度
            压栈 r矩形.底
            出栈 n窗口高度
            
            //将窗口高度减去工具条与状态条高度
            累加32 =  n工具条高度
            n窗口高度 -= 累加32  //sub n窗口高度,eax
            累加32 =  n状态条高度
            n窗口高度 -= 累加32  //sub n窗口高度,eax

            n工具条高度 += 2  //add n工具条高度,2
            n窗口高度 -= 2    //sub n窗口高度,2
            
            //调整编辑框位置
            移动窗体(h编辑框句柄,0,n工具条高度,n窗口宽度,n窗口高度,真) //MoveWindow
        }
        否则
        {
            默认窗体过程(句柄,消息,消息参数一,消息参数二)  //把"默认窗口过程"改成"默认窗体过程"
            返回
        }

        累加32^=累加32  //xor eax,eax
    } 

}//代码段结束


